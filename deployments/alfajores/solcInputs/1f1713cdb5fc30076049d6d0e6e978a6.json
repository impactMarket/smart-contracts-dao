{
  "language": "Solidity",
  "sources": {
    "contracts/governor/interfaces/IPCTDelegateStorageV1.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\nimport \"@ubeswap/governance/contracts/interfaces/IHasVotes.sol\";\nimport \"./IPCTDelegatorStorage.sol\";\nimport \"./TimelockInterface.sol\";\n\n/**\n * @title Storage for Governor Delegate\n * @notice For future upgrades, do not change IPCTDelegateStorageV1. Create a new\n * contract which implements IPCTDelegateStorageV1 and following the naming convention\n * IPCTDelegateStorageVX.\n */\ncontract IPCTDelegateStorageV1 is IPCTDelegatorStorage {\n    /// @notice The delay before voting on a proposal may take place, once proposed, in blocks\n    uint256 public votingDelay;\n\n    /// @notice The duration of voting on a proposal, in blocks\n    uint256 public votingPeriod;\n\n    /// @notice The number of votes required in order for a voter to become a proposer\n    uint256 public proposalThreshold;\n\n    /// @notice The total number of proposals\n    uint256 public proposalCount;\n\n    /// @notice The number of votes in support of a proposal required in order for a quorum to be reached and for a vote to succeed\n    uint256 public quorumVotes;\n\n    /// @notice The address of the Governance Timelock\n    TimelockInterface public timelock;\n\n    /// @notice The address of the governance token\n    IHasVotes public token;\n\n    /// @notice The address of the \"Release\" governance token\n    IHasVotes public releaseToken;\n\n    /// @notice The official record of all proposals ever proposed\n    mapping(uint256 => Proposal) public proposals;\n    /// @notice The official each proposal's targets:\n    /// An ordered list of target addresses for calls to be made\n    mapping(uint256 => address[]) public proposalTargets;\n    /// @notice The official each proposal's values:\n    /// An ordered list of values (i.e. msg.value) to be passed to the calls to be made\n    mapping(uint256 => uint256[]) public proposalValues;\n    /// @notice The official each proposal's signatures:\n    /// An ordered list of function signatures to be called\n    mapping(uint256 => string[]) public proposalSignatures;\n    /// @notice The official each proposal's calldatas:\n    /// An ordered list of calldata to be passed to each call\n    mapping(uint256 => bytes[]) public proposalCalldatas;\n    /// @notice The official each proposal's receipts:\n    /// Receipts of ballots for the entire set of voters\n    mapping(uint256 => mapping(address => Receipt)) public proposalReceipts;\n\n    /// @notice The latest proposal for each proposer\n    mapping(address => uint256) public latestProposalIds;\n\n    struct Proposal {\n        // Unique id for looking up a proposal\n        uint256 id;\n        // Creator of the proposal\n        address proposer;\n        // The timestamp that the proposal will be available for execution, set once the vote succeeds\n        uint256 eta;\n        // The block at which voting begins: holders must delegate their votes prior to this block\n        uint256 startBlock;\n        // The block at which voting ends: votes must be cast prior to this block\n        uint256 endBlock;\n        // Current number of votes in favor of this proposal\n        uint256 forVotes;\n        // Current number of votes in opposition to this proposal\n        uint256 againstVotes;\n        // Current number of votes for abstaining for this proposal\n        uint256 abstainVotes;\n        // Flag marking whether the proposal has been canceled\n        bool canceled;\n        // Flag marking whether the proposal has been executed\n        bool executed;\n    }\n\n    /// @notice Ballot receipt record for a voter\n    struct Receipt {\n        // Whether or not a vote has been cast\n        bool hasVoted;\n        // Whether or not the voter supports the proposal or abstains\n        uint8 support;\n        // The number of votes the voter had, which were cast\n        uint96 votes;\n    }\n\n    /// @notice Possible states that a proposal may be in\n    enum ProposalState {\n        Pending,\n        Active,\n        Canceled,\n        Defeated,\n        Succeeded,\n        Queued,\n        Expired,\n        Executed\n    }\n}\n"
    },
    "@ubeswap/governance/contracts/interfaces/IHasVotes.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\n/**\n * Reads the votes that an account has.\n */\ninterface IHasVotes {\n    /**\n     * @notice Gets the current votes balance for `account`\n     * @param account The address to get votes balance\n     * @return The number of current votes for `account`\n     */\n    function getCurrentVotes(address account) external view returns (uint96);\n\n    /**\n     * @notice Determine the prior number of votes for an account as of a block number\n     * @dev Block number must be a finalized block or else this function will revert to prevent misinformation.\n     * @param account The address of the account to check\n     * @param blockNumber The block number to get the vote balance at\n     * @return The number of votes the account had as of the given block\n     */\n    function getPriorVotes(address account, uint256 blockNumber)\n        external\n        view\n        returns (uint96);\n}\n"
    },
    "contracts/governor/interfaces/IPCTDelegatorStorage.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\ncontract IPCTDelegatorStorage {\n  /// @notice Administrator for this contract\n  address public admin;\n\n  /// @notice Pending administrator for this contract\n  address public pendingAdmin;\n\n  /// @notice Active brains of Governor\n  address public implementation;\n}\n"
    },
    "contracts/governor/interfaces/TimelockInterface.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\ninterface TimelockInterface {\n  function admin() external view returns (address);\n\n  function delay() external view returns (uint256);\n\n  function GRACE_PERIOD() external view returns (uint256);\n\n  function acceptAdmin() external;\n\n  function queuedTransactions(bytes32 hash) external view returns (bool);\n\n  function queueTransaction(\n    address target,\n    uint256 value,\n    string calldata signature,\n    bytes calldata data,\n    uint256 eta\n  ) external returns (bytes32);\n\n  function cancelTransaction(\n    address target,\n    uint256 value,\n    string calldata signature,\n    bytes calldata data,\n    uint256 eta\n  ) external;\n\n  function executeTransaction(\n    address target,\n    uint256 value,\n    string calldata signature,\n    bytes calldata data,\n    uint256 eta\n  ) external payable returns (bytes memory);\n}\n"
    },
    "contracts/governor/IPCTDelegate.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\nimport \"@openzeppelin/contracts/proxy/utils/Initializable.sol\";\nimport \"./interfaces/IPCTDelegateStorageV1.sol\";\nimport \"./interfaces/IPCTEvents.sol\";\n\ncontract IPCTDelegate is IPCTDelegateStorageV1, IPCTEvents, Initializable {\n    /// @notice The name of this contract\n    string public constant NAME = \"IPCT\";\n\n    /// @notice The minimum setable proposal threshold\n    uint256 public constant MIN_PROPOSAL_THRESHOLD = 100_000_000e18; // 100,000,000 Tokens\n\n    /// @notice The maximum setable proposal threshold\n    uint256 public constant MAX_PROPOSAL_THRESHOLD = 500_000_000e18; // 500,000,000 Tokens\n\n    /// @notice The minimum setable voting period\n    uint256 public constant MIN_VOTING_PERIOD = 17280; // About 24 hours\n\n    /// @notice The max setable voting period\n    uint256 public constant MAX_VOTING_PERIOD = 241920; // About 2 weeks\n\n    /// @notice The min setable voting delay\n    uint256 public constant MIN_VOTING_DELAY = 1;\n\n    /// @notice The max setable voting delay\n    uint256 public constant MAX_VOTING_DELAY = 120960; // About 1 week\n\n    /// @notice The maximum number of actions that can be included in a proposal\n    uint256 public constant PROPOSAL_MAX_OPERATIONS = 10; // 10 actions\n\n    /// @notice The EIP-712 typehash for the contract's domain\n    bytes32 public constant DOMAIN_TYPEHASH =\n        keccak256(\"EIP712Domain(string name,uint256 chainId,address verifyingContract)\");\n\n    /// @notice The EIP-712 typehash for the ballot struct used by the contract\n    bytes32 public constant BALLOT_TYPEHASH = keccak256(\"Ballot(uint256 proposalId,uint8 support)\");\n\n    modifier adminOnly() {\n        require(msg.sender == admin, \"Only admin can call\");\n        _;\n    }\n\n    /**\n     * @notice Used to initialize the contract during delegator contructor\n     * @param timelock_ The address of the Timelock\n     * @param token_ The address of the voting token\n     * @param releaseToken_ The address of the \"Release\" voting token. If none, specify the zero address.\n     * @param votingPeriod_ The initial voting period\n     * @param votingDelay_ The initial voting delay\n     * @param proposalThreshold_ The initial proposal threshold\n     * @param quorumVotes_ The initial quorum votes\n     */\n    function initialize(\n        address timelock_,\n        address token_,\n        address releaseToken_,\n        uint256 votingPeriod_,\n        uint256 votingDelay_,\n        uint256 proposalThreshold_,\n        uint256 quorumVotes_\n    ) public initializer adminOnly {\n        require(\n            TimelockInterface(timelock_).admin() == address(this),\n            \"IPCT::initialize: timelock admin is not assigned to IPCTDelegate\"\n        );\n        require(\n            votingPeriod_ >= MIN_VOTING_PERIOD && votingPeriod_ <= MAX_VOTING_PERIOD,\n            \"IPCT::initialize: invalid voting period\"\n        );\n        require(\n            votingDelay_ >= MIN_VOTING_DELAY && votingDelay_ <= MAX_VOTING_DELAY,\n            \"IPCT::initialize: invalid voting delay\"\n        );\n        require(\n            proposalThreshold_ >= MIN_PROPOSAL_THRESHOLD &&\n                proposalThreshold_ <= MAX_PROPOSAL_THRESHOLD,\n            \"IPCT::initialize: invalid proposal threshold\"\n        );\n        require(quorumVotes_ >= proposalThreshold_, \"IPCT::initialize: invalid quorum votes\");\n        timelock = TimelockInterface(timelock_);\n        require(\n            timelock.admin() == address(this),\n            \"IPCT::initialize: timelock admin is not assigned to IPCTDelegate\"\n        );\n\n        admin = msg.sender;\n        token = IHasVotes(token_);\n        releaseToken = IHasVotes(releaseToken_);\n        votingPeriod = votingPeriod_;\n        votingDelay = votingDelay_;\n        proposalThreshold = proposalThreshold_;\n        quorumVotes = quorumVotes_;\n\n        // Create dummy proposal\n        Proposal memory dummyProposal = Proposal({\n            id: proposalCount,\n            proposer: address(this),\n            eta: 0,\n            startBlock: 0,\n            endBlock: 0,\n            forVotes: 0,\n            againstVotes: 0,\n            abstainVotes: 0,\n            canceled: true,\n            executed: false\n        });\n        proposalCount++;\n\n        proposals[dummyProposal.id] = dummyProposal;\n        latestProposalIds[dummyProposal.proposer] = dummyProposal.id;\n\n        emit ProposalCreated(\n            dummyProposal.id,\n            address(this),\n            proposalTargets[dummyProposal.id],\n            proposalValues[dummyProposal.id],\n            proposalSignatures[dummyProposal.id],\n            proposalCalldatas[dummyProposal.id],\n            0,\n            0,\n            \"\"\n        );\n    }\n\n    /**\n     * @notice Function used to propose a new proposal. Sender must have delegates above the proposal threshold.\n     * @param targets Target addresses for proposal calls.\n     * @param values Eth values for proposal calls.\n     * @param signatures Function signatures for proposal calls.\n     * @param calldatas Calldatas for proposal calls.\n     * @param description String description of the proposal.\n     * @return Proposal id of new proposal.\n     */\n    function propose(\n        address[] memory targets,\n        uint256[] memory values,\n        string[] memory signatures,\n        bytes[] memory calldatas,\n        string memory description\n    ) public returns (uint256) {\n        require(\n            getPriorVotes(msg.sender, sub256(block.number, 1)) > proposalThreshold,\n            \"IPCT::propose: proposer votes below proposal threshold\"\n        );\n        require(\n            targets.length == values.length &&\n                targets.length == signatures.length &&\n                targets.length == calldatas.length,\n            \"IPCT::propose: proposal function information arity mismatch\"\n        );\n        require(targets.length != 0, \"IPCT::propose: must provide actions\");\n        require(targets.length <= PROPOSAL_MAX_OPERATIONS, \"IPCT::propose: too many actions\");\n\n        uint256 latestProposalId = latestProposalIds[msg.sender];\n        if (latestProposalId != 0) {\n            ProposalState proposersLatestProposalState = state(latestProposalId);\n            require(\n                proposersLatestProposalState != ProposalState.Active,\n                \"IPCT::propose: one live proposal per proposer, found an already active proposal\"\n            );\n            require(\n                proposersLatestProposalState != ProposalState.Pending,\n                \"IPCT::propose: one live proposal per proposer, found an already pending proposal\"\n            );\n        }\n\n        uint256 startBlock = add256(block.number, votingDelay);\n        uint256 endBlock = add256(startBlock, votingPeriod);\n\n        Proposal memory newProposal = Proposal({\n            id: proposalCount,\n            proposer: msg.sender,\n            eta: 0,\n            startBlock: startBlock,\n            endBlock: endBlock,\n            forVotes: 0,\n            againstVotes: 0,\n            abstainVotes: 0,\n            canceled: false,\n            executed: false\n        });\n        proposalCount++;\n\n        proposals[newProposal.id] = newProposal;\n        proposalTargets[newProposal.id] = targets;\n        proposalValues[newProposal.id] = values;\n        proposalSignatures[newProposal.id] = signatures;\n        proposalCalldatas[newProposal.id] = calldatas;\n        latestProposalIds[newProposal.proposer] = newProposal.id;\n\n        emit ProposalCreated(\n            newProposal.id,\n            msg.sender,\n            targets,\n            values,\n            signatures,\n            calldatas,\n            startBlock,\n            endBlock,\n            description\n        );\n        return newProposal.id;\n    }\n\n    /**\n     * @notice Queues a proposal of state succeeded\n     * @param proposalId The id of the proposal to queue\n     */\n    function queue(uint256 proposalId) external {\n        require(\n            state(proposalId) == ProposalState.Succeeded,\n            \"IPCT::queue: proposal can only be queued if it is succeeded\"\n        );\n        Proposal storage proposal = proposals[proposalId];\n        uint256 eta = add256(block.timestamp, timelock.delay());\n        for (uint256 i = 0; i < proposalTargets[proposalId].length; i++) {\n            queueOrRevertInternal(\n                proposalTargets[proposalId][i],\n                proposalValues[proposalId][i],\n                proposalSignatures[proposalId][i],\n                proposalCalldatas[proposalId][i],\n                eta\n            );\n        }\n        proposal.eta = eta;\n        emit ProposalQueued(proposalId, eta);\n    }\n\n    function queueOrRevertInternal(\n        address target,\n        uint256 value,\n        string memory signature,\n        bytes memory data,\n        uint256 eta\n    ) internal {\n        require(\n            !timelock.queuedTransactions(\n                keccak256(abi.encode(target, value, signature, data, eta))\n            ),\n            \"IPCT::queueOrRevertInternal: identical proposal action already queued at eta\"\n        );\n        timelock.queueTransaction(target, value, signature, data, eta);\n    }\n\n    /**\n     * @notice Executes a queued proposal if eta has passed\n     * @param proposalId The id of the proposal to execute\n     */\n    function execute(uint256 proposalId) external payable {\n        require(\n            state(proposalId) == ProposalState.Queued,\n            \"IPCT::execute: proposal can only be executed if it is queued\"\n        );\n        Proposal storage proposal = proposals[proposalId];\n        proposal.executed = true;\n        for (uint256 i = 0; i < proposalTargets[proposalId].length; i++) {\n            timelock.executeTransaction{value: proposalValues[proposalId][i]}(\n                proposalTargets[proposalId][i],\n                proposalValues[proposalId][i],\n                proposalSignatures[proposalId][i],\n                proposalCalldatas[proposalId][i],\n                proposal.eta\n            );\n        }\n        emit ProposalExecuted(proposalId);\n    }\n\n    /**\n     * @notice Cancels a proposal only if sender is the proposer, or proposer delegates dropped below proposal threshold\n     * @param proposalId The id of the proposal to cancel\n     */\n    function cancel(uint256 proposalId) external {\n        require(\n            state(proposalId) != ProposalState.Executed,\n            \"IPCT::cancel: cannot cancel executed proposal\"\n        );\n\n        Proposal storage proposal = proposals[proposalId];\n        require(\n            msg.sender == proposal.proposer ||\n                getPriorVotes(proposal.proposer, sub256(block.number, 1)) < proposalThreshold,\n            \"IPCT::cancel: proposer above threshold\"\n        );\n\n        proposal.canceled = true;\n        for (uint256 i = 0; i < proposalTargets[proposalId].length; i++) {\n            timelock.cancelTransaction(\n                proposalTargets[proposalId][i],\n                proposalValues[proposalId][i],\n                proposalSignatures[proposalId][i],\n                proposalCalldatas[proposalId][i],\n                proposal.eta\n            );\n        }\n\n        emit ProposalCanceled(proposalId);\n    }\n\n    /**\n     * @notice Gets actions of a proposal.\n     * @param proposalId Proposal to query.\n     * @return targets Target addresses for proposal calls.\n     * @return values Eth values for proposal calls.\n     * @return signatures Function signatures for proposal calls.\n     * @return calldatas Calldatas for proposal calls.\n     */\n    function getActions(uint256 proposalId)\n        external\n        view\n        returns (\n            address[] memory targets,\n            uint256[] memory values,\n            string[] memory signatures,\n            bytes[] memory calldatas\n        )\n    {\n        return (\n            proposalTargets[proposalId],\n            proposalValues[proposalId],\n            proposalSignatures[proposalId],\n            proposalCalldatas[proposalId]\n        );\n    }\n\n    /**\n     * @notice Gets the receipt for a voter on a given proposal\n     * @param proposalId the id of proposal\n     * @param voter The address of the voter\n     * @return The voting receipt\n     */\n    function getReceipt(uint256 proposalId, address voter) external view returns (Receipt memory) {\n        return proposalReceipts[proposalId][voter];\n    }\n\n    /**\n     * @notice Gets the state of a proposal\n     * @param proposalId The id of the proposal\n     * @return Proposal state\n     */\n    function state(uint256 proposalId) public view returns (ProposalState) {\n        require(proposalCount > proposalId, \"IPCT::state: invalid proposal id\");\n        Proposal storage proposal = proposals[proposalId];\n\n        if (proposal.canceled) {\n            return ProposalState.Canceled;\n        } else if (block.number <= proposal.startBlock) {\n            return ProposalState.Pending;\n        } else if (block.number <= proposal.endBlock) {\n            return ProposalState.Active;\n        } else if (proposal.forVotes <= proposal.againstVotes || proposal.forVotes < quorumVotes) {\n            return ProposalState.Defeated;\n        } else if (proposal.eta == 0) {\n            return ProposalState.Succeeded;\n        } else if (proposal.executed) {\n            return ProposalState.Executed;\n        } else if (block.timestamp >= add256(proposal.eta, timelock.GRACE_PERIOD())) {\n            return ProposalState.Expired;\n        } else {\n            return ProposalState.Queued;\n        }\n    }\n\n    /**\n     * @notice Cast a vote for a proposal\n     * @param proposalId The id of the proposal to vote on\n     * @param support The support value for the vote. 0=against, 1=for, 2=abstain\n     */\n    function castVote(uint256 proposalId, uint8 support) external {\n        emit VoteCast(\n            msg.sender,\n            proposalId,\n            support,\n            castVoteInternal(msg.sender, proposalId, support),\n            \"\"\n        );\n    }\n\n    /**\n     * @notice Cast a vote for a proposal with a reason\n     * @param proposalId The id of the proposal to vote on\n     * @param support The support value for the vote. 0=against, 1=for, 2=abstain\n     * @param reason The reason given for the vote by the voter\n     */\n    function castVoteWithReason(\n        uint256 proposalId,\n        uint8 support,\n        string calldata reason\n    ) external {\n        emit VoteCast(\n            msg.sender,\n            proposalId,\n            support,\n            castVoteInternal(msg.sender, proposalId, support),\n            reason\n        );\n    }\n\n    /**\n     * @notice Cast a vote for a proposal by signature\n     * @dev External function that accepts EIP-712 signatures for voting on proposals.\n     */\n    function castVoteBySig(\n        uint256 proposalId,\n        uint8 support,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) external {\n        bytes32 domainSeparator = keccak256(\n            abi.encode(DOMAIN_TYPEHASH, keccak256(bytes(NAME)), getChainIdInternal(), address(this))\n        );\n        bytes32 structHash = keccak256(abi.encode(BALLOT_TYPEHASH, proposalId, support));\n        bytes32 digest = keccak256(abi.encodePacked(\"\\x19\\x01\", domainSeparator, structHash));\n        address signatory = ecrecover(digest, v, r, s);\n        require(signatory != address(0), \"IPCT::castVoteBySig: invalid signature\");\n        emit VoteCast(\n            signatory,\n            proposalId,\n            support,\n            castVoteInternal(signatory, proposalId, support),\n            \"\"\n        );\n    }\n\n    /**\n     * @notice Internal function that caries out voting logic\n     * @param voter The voter that is casting their vote\n     * @param proposalId The id of the proposal to vote on\n     * @param support The support value for the vote. 0=against, 1=for, 2=abstain\n     * @return The number of votes cast\n     */\n    function castVoteInternal(\n        address voter,\n        uint256 proposalId,\n        uint8 support\n    ) internal returns (uint96) {\n        require(\n            state(proposalId) == ProposalState.Active,\n            \"IPCT::castVoteInternal: voting is closed\"\n        );\n        require(support <= 2, \"IPCT::castVoteInternal: invalid vote type\");\n        Proposal storage proposal = proposals[proposalId];\n        Receipt storage receipt = proposalReceipts[proposalId][voter];\n        require(receipt.hasVoted == false, \"IPCT::castVoteInternal: voter already voted\");\n        uint96 votes = getPriorVotes(voter, proposal.startBlock);\n\n        if (support == 0) {\n            proposal.againstVotes = add256(proposal.againstVotes, votes);\n        } else if (support == 1) {\n            proposal.forVotes = add256(proposal.forVotes, votes);\n        } else if (support == 2) {\n            proposal.abstainVotes = add256(proposal.abstainVotes, votes);\n        }\n\n        receipt.hasVoted = true;\n        receipt.support = support;\n        receipt.votes = votes;\n\n        return votes;\n    }\n\n    /**\n     * @notice Admin function for setting the voting delay\n     * @param newVotingDelay new voting delay, in blocks\n     */\n    function _setVotingDelay(uint256 newVotingDelay) external virtual adminOnly {\n        require(\n            newVotingDelay >= MIN_VOTING_DELAY && newVotingDelay <= MAX_VOTING_DELAY,\n            \"IPCT::_setVotingDelay: invalid voting delay\"\n        );\n        uint256 oldVotingDelay = votingDelay;\n        votingDelay = newVotingDelay;\n\n        emit VotingDelaySet(oldVotingDelay, newVotingDelay);\n    }\n\n    /**\n     * @notice Admin function for setting the quorum votes\n     * @param newQuorumVotes new quorum votes\n     */\n    function _setQuorumVotes(uint256 newQuorumVotes) external adminOnly {\n        require(newQuorumVotes >= proposalThreshold, \"IPCT::_setQuorumVotes: invalid quorum votes\");\n\n        uint256 oldQuorumVotes = votingDelay;\n        quorumVotes = newQuorumVotes;\n\n        emit QuorumVotesSet(oldQuorumVotes, newQuorumVotes);\n    }\n\n    /**\n     * @notice Admin function for setting the voting period\n     * @param newVotingPeriod new voting period, in blocks\n     */\n    function _setVotingPeriod(uint256 newVotingPeriod) external virtual adminOnly {\n        require(\n            newVotingPeriod >= MIN_VOTING_PERIOD && newVotingPeriod <= MAX_VOTING_PERIOD,\n            \"IPCT::_setVotingPeriod: invalid voting period\"\n        );\n        uint256 oldVotingPeriod = votingPeriod;\n        votingPeriod = newVotingPeriod;\n\n        emit VotingPeriodSet(oldVotingPeriod, newVotingPeriod);\n    }\n\n    /**\n     * @notice Admin function for setting the proposal threshold\n     * @dev newProposalThreshold must be greater than the hardcoded min\n     * @param newProposalThreshold new proposal threshold\n     */\n    function _setProposalThreshold(uint256 newProposalThreshold) external adminOnly {\n        require(\n            newProposalThreshold >= MIN_PROPOSAL_THRESHOLD &&\n                newProposalThreshold <= MAX_PROPOSAL_THRESHOLD,\n            \"IPCT::_setProposalThreshold: invalid proposal threshold\"\n        );\n        uint256 oldProposalThreshold = proposalThreshold;\n        proposalThreshold = newProposalThreshold;\n\n        emit ProposalThresholdSet(oldProposalThreshold, newProposalThreshold);\n    }\n\n    /**\n     * @notice Begins transfer of admin rights. The newPendingAdmin must call `_acceptAdmin` to finalize the transfer.\n     * @dev Admin function to begin change of admin. The newPendingAdmin must call `_acceptAdmin` to finalize the transfer.\n     * @param newPendingAdmin New pending admin.\n     */\n    function _setPendingAdmin(address newPendingAdmin) external adminOnly {\n        // Save current value, if any, for inclusion in log\n        address oldPendingAdmin = pendingAdmin;\n\n        // Store pendingAdmin with value newPendingAdmin\n        pendingAdmin = newPendingAdmin;\n\n        // Emit NewPendingAdmin(oldPendingAdmin, newPendingAdmin)\n        emit NewPendingAdmin(oldPendingAdmin, newPendingAdmin);\n    }\n\n    /**\n     * @notice Accepts transfer of admin rights. msg.sender must be pendingAdmin\n     * @dev Admin function for pending admin to accept role and update admin\n     */\n    function _acceptAdmin() external {\n        // Check caller is pendingAdmin and pendingAdmin â‰  address(0)\n        require(\n            msg.sender == pendingAdmin && msg.sender != address(0),\n            \"IPCT:_acceptAdmin: pending admin only\"\n        );\n\n        // Save current values for inclusion in log\n        address oldAdmin = admin;\n        address oldPendingAdmin = pendingAdmin;\n\n        // Store admin with value pendingAdmin\n        admin = pendingAdmin;\n\n        // Clear the pending value\n        pendingAdmin = address(0);\n\n        emit NewAdmin(oldAdmin, admin);\n        emit NewPendingAdmin(oldPendingAdmin, address(0));\n    }\n\n    function add256(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"addition overflow\");\n        return c;\n    }\n\n    function sub256(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b <= a, \"subtraction underflow\");\n        return a - b;\n    }\n\n    function getChainIdInternal() internal view returns (uint256) {\n        uint256 chainId;\n        assembly {\n            chainId := chainid()\n        }\n        return chainId;\n    }\n\n    function getPriorVotes(address voter, uint256 beforeBlock) internal view returns (uint96) {\n        if (address(releaseToken) == address(0)) {\n            return token.getPriorVotes(voter, beforeBlock);\n        }\n        return\n            add96(\n                token.getPriorVotes(voter, beforeBlock),\n                releaseToken.getPriorVotes(voter, beforeBlock),\n                \"getPriorVotes overflow\"\n            );\n    }\n\n    function add96(\n        uint96 a,\n        uint96 b,\n        string memory errorMessage\n    ) internal pure returns (uint96) {\n        uint96 c = a + b;\n        require(c >= a, errorMessage);\n        return c;\n    }\n}\n"
    },
    "@openzeppelin/contracts/proxy/utils/Initializable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.0;\n\n/**\n * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed\n * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an\n * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer\n * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.\n *\n * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as\n * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.\n *\n * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure\n * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.\n */\nabstract contract Initializable {\n    /**\n     * @dev Indicates that the contract has been initialized.\n     */\n    bool private _initialized;\n\n    /**\n     * @dev Indicates that the contract is in the process of being initialized.\n     */\n    bool private _initializing;\n\n    /**\n     * @dev Modifier to protect an initializer function from being invoked twice.\n     */\n    modifier initializer() {\n        require(_initializing || !_initialized, \"Initializable: contract is already initialized\");\n\n        bool isTopLevelCall = !_initializing;\n        if (isTopLevelCall) {\n            _initializing = true;\n            _initialized = true;\n        }\n\n        _;\n\n        if (isTopLevelCall) {\n            _initializing = false;\n        }\n    }\n}\n"
    },
    "contracts/governor/interfaces/IPCTEvents.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\ncontract IPCTEvents {\n    /// @notice An event emitted when a new proposal is created\n    event ProposalCreated(\n        uint256 id,\n        address proposer,\n        address[] targets,\n        uint256[] values,\n        string[] signatures,\n        bytes[] calldatas,\n        uint256 startBlock,\n        uint256 endBlock,\n        string description\n    );\n\n    /// @notice An event emitted when a vote has been cast on a proposal\n    /// @param voter The address which casted a vote\n    /// @param proposalId The proposal id which was voted on\n    /// @param support Support value for the vote. 0=against, 1=for, 2=abstain\n    /// @param votes Number of votes which were cast by the voter\n    /// @param reason The reason given for the vote by the voter\n    event VoteCast(address indexed voter, uint256 proposalId, uint8 support, uint256 votes, string reason);\n\n    /// @notice An event emitted when a proposal has been canceled\n    event ProposalCanceled(uint256 id);\n\n    /// @notice An event emitted when a proposal has been queued in the Timelock\n    event ProposalQueued(uint256 id, uint256 eta);\n\n    /// @notice An event emitted when a proposal has been executed in the Timelock\n    event ProposalExecuted(uint256 id);\n\n    /// @notice An event emitted when the voting delay is set\n    event VotingDelaySet(uint256 oldVotingDelay, uint256 newVotingDelay);\n\n    /// @notice An event emitted when the voting period is set\n    event VotingPeriodSet(uint256 oldVotingPeriod, uint256 newVotingPeriod);\n\n    /// @notice Emitted when implementation is changed\n    event NewImplementation(address oldImplementation, address newImplementation);\n\n    /// @notice Emitted when proposal threshold is set\n    event ProposalThresholdSet(uint256 oldProposalThreshold, uint256 newProposalThreshold);\n\n    /// @notice An event emitted when the quorum votes is set\n    event QuorumVotesSet(uint256 oldQuorumVotes, uint256 newQuorumVotes);\n\n    /// @notice Emitted when pendingAdmin is changed\n    event NewPendingAdmin(address oldPendingAdmin, address newPendingAdmin);\n\n    /// @notice Emitted when pendingAdmin is accepted, which means admin is updated\n    event NewAdmin(address oldAdmin, address newAdmin);\n}\n"
    },
    "contracts/mocks/IPCTDelegateMock.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\nimport \"../governor/IPCTDelegate.sol\";\n\n//used only for testing\ncontract IPCTDelegateMock is IPCTDelegate {\n    /**\n     * @notice Function for setting the voting period\n     * @param newVotingPeriod new voting period, in blocks\n     */\n    function _setVotingPeriod(uint256 newVotingPeriod) external override {\n        uint256 oldVotingPeriod = votingPeriod;\n        votingPeriod = newVotingPeriod;\n\n        emit VotingPeriodSet(oldVotingPeriod, votingPeriod);\n    }\n\n    /**\n     * @notice Admin function for setting the voting delay\n     * @param newVotingDelay new voting delay, in blocks\n     */\n    function _setVotingDelay(uint256 newVotingDelay) external override {\n        uint256 oldVotingDelay = votingDelay;\n        votingDelay = newVotingDelay;\n\n        emit VotingDelaySet(oldVotingDelay, votingDelay);\n    }\n}\n"
    },
    "contracts/governor/IPCTDelegator.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\nimport \"./interfaces/IPCTDelegatorStorage.sol\";\nimport \"./interfaces/IPCTEvents.sol\";\n\ncontract IPCTDelegator is IPCTDelegatorStorage, IPCTEvents {\n    constructor(\n        address timelock_,\n        address token_,\n        address releaseToken_,\n        address admin_,\n        address implementation_,\n        uint256 votingPeriod_,\n        uint256 votingDelay_,\n        uint256 proposalThreshold_,\n        uint256 quorumVotes_\n    ) {\n        // Admin set to msg.sender for initialization\n        admin = msg.sender;\n\n        delegateTo(\n            implementation_,\n            abi.encodeWithSignature(\n                \"initialize(address,address,address,uint256,uint256,uint256,uint256)\",\n                timelock_,\n                token_,\n                releaseToken_,\n                votingPeriod_,\n                votingDelay_,\n                proposalThreshold_,\n                quorumVotes_\n            )\n        );\n        _setImplementation(implementation_);\n        admin = admin_;\n    }\n\n    /**\n     * @notice Called by the admin to update the implementation of the delegator\n     * @param implementation_ The address of the new implementation for delegation\n     */\n    function _setImplementation(address implementation_) public {\n        require(msg.sender == admin, \"IPCTDelegator::_setImplementation: admin only\");\n        require(\n            implementation_ != address(0),\n            \"IPCTDelegator::_setImplementation: invalid implementation address\"\n        );\n\n        address oldImplementation = implementation;\n        implementation = implementation_;\n\n        emit NewImplementation(oldImplementation, implementation_);\n    }\n\n    /**\n     * @notice Internal method to delegate execution to another contract\n     * @dev It returns to the external caller whatever the implementation returns or forwards reverts\n     * @param callee The contract to delegatecall\n     * @param data The raw data to delegatecall\n     */\n    function delegateTo(address callee, bytes memory data) internal {\n        (bool success, bytes memory returnData) = callee.delegatecall(data);\n        assembly {\n            if eq(success, 0) {\n                revert(add(returnData, 0x20), returndatasize())\n            }\n        }\n    }\n\n    /**\n     * @dev Delegates execution to an implementation contract.\n     * It returns to the external caller whatever the implementation returns\n     * or forwards reverts.\n     */\n    fallback() external {\n        // delegate all other functions to current implementation\n        (bool success, ) = implementation.delegatecall(msg.data);\n\n        assembly {\n            let free_mem_ptr := mload(0x40)\n            returndatacopy(free_mem_ptr, 0, returndatasize())\n\n            switch success\n            case 0 {\n                revert(free_mem_ptr, returndatasize())\n            }\n            default {\n                return(free_mem_ptr, returndatasize())\n            }\n        }\n    }\n}\n"
    },
    "@ubeswap/governance/contracts/voting/VotingPower.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\nimport \"../interfaces/IHasVotes.sol\";\nimport \"../interfaces/IVotingDelegates.sol\";\n/**\n * Power to vote. Heavily based on Uni.\n */\ncontract VotingPower is IHasVotes, IVotingDelegates {\n    // Name of the token. This cannot be changed after creating the token.\n    string private _name;\n\n    // Total amount of voting power available.\n    uint96 private totalVotingPowerSupply;\n\n    constructor(string memory name_) {\n        _name = name_;\n    }\n\n    function name() public view virtual override returns (string memory) {\n        return _name;\n    }\n\n    /**\n     * @notice Mint new voting power\n     * @param dst The address of the destination account\n     * @param amount The amount of voting power to be minted\n     */\n    function _mintVotes(address dst, uint96 amount) internal {\n        require(dst != address(0), \"VotingPower::_mintVotes: cannot mint to the zero address\");\n\n        // transfer the amount to the recipient\n        balances[dst] = add96(balances[dst], amount, \"VotingPower::_mintVotes: mint amount overflows\");\n        totalVotingPowerSupply = add96(\n            totalVotingPowerSupply, amount, \"VotingPower::_mintVotes: total supply overflows\"\n        );\n        emit Transfer(address(0), dst, amount);\n\n        // move delegates\n        _moveDelegates(address(0), delegates[dst], amount);\n    }\n\n    /**\n     * @notice Burn voting power\n     * @param src The address of the source account\n     * @param amount The amount of voting power to be burned\n     */\n    function _burnVotes(address src, uint96 amount) internal {\n        require(src != address(0), \"VotingPower::_burnVotes: cannot burn from the zero address\");\n\n        // transfer the amount to the recipient\n        balances[src] = sub96(balances[src], amount, \"VotingPower::_burnVotes: burn amount underflows\");\n        totalVotingPowerSupply = sub96(\n            totalVotingPowerSupply, amount, \"VotingPower::_burnVotes: total supply underflows\"\n        );\n        emit Transfer(src, address(0), amount);\n\n        // move delegates\n        _moveDelegates(delegates[src], address(0), amount);\n    }\n\n    /**\n     * @notice Get the amount of voting power of an account\n     * @param account The address of the account to get the balance of\n     * @return The amount of voting power held\n     */\n    function votingPower(address account) public view override returns (uint96) {\n        return balances[account];\n    }\n\n    function totalVotingPower() public view override returns (uint96) {\n        return totalVotingPowerSupply;\n    }\n\n    ////////////////////////////////\n    //\n    // The below code is copied from ../uniswap-governance/contracts/Uni.sol.\n    // Changes are marked with \"XXX\".\n    //\n    ////////////////////////////////\n\n    // XXX: deleted name, symbol, decimals, totalSupply, minter, mintingAllowedAfter,\n    // minimumTimeBetweenMints, mintCap, allowances\n\n    // Official record of token balances for each account\n    // XXX: internal => private visibility\n    mapping (address => uint96) private balances;\n\n    /// @notice A record of each accounts delegate\n    mapping (address => address) public override delegates;\n\n    /// @notice A checkpoint for marking number of votes from a given block\n    struct Checkpoint {\n        uint32 fromBlock;\n        uint96 votes;\n    }\n\n    /// @notice A record of votes checkpoints for each account, by index\n    mapping (address => mapping (uint32 => Checkpoint)) public checkpoints;\n\n    /// @notice The number of checkpoints for each account\n    mapping (address => uint32) public numCheckpoints;\n\n    /// @notice The EIP-712 typehash for the contract's domain\n    bytes32 public constant DOMAIN_TYPEHASH = keccak256(\"EIP712Domain(string name,uint256 chainId,address verifyingContract)\");\n\n    /// @notice The EIP-712 typehash for the delegation struct used by the contract\n    bytes32 public constant DELEGATION_TYPEHASH = keccak256(\"Delegation(address delegatee,uint256 nonce,uint256 expiry)\");\n\n    // XXX: deleted PERMIT_TYPEHASH\n\n    /// @notice A record of states for signing / validating signatures\n    mapping (address => uint) public nonces;\n\n    // XXX: deleted MinterChanged\n\n    // XXX: deleted DelegateChanged, DelegateVotesChanged, Transfer and moved them to IVotingPower\n\n    // XXX: deleted Approval\n\n    // XXX: deleted constructor, setMinter, mint, allowance, approve, permit, balanceOf\n\n    // XXX: deleted transfer, transferFrom\n\n    /**\n     * @notice Delegate votes from `msg.sender` to `delegatee`\n     * @param delegatee The address to delegate votes to\n     */\n    function delegate(address delegatee) public override {\n        return _delegate(msg.sender, delegatee);\n    }\n\n    /**\n     * @notice Delegates votes from signatory to `delegatee`\n     * @param delegatee The address to delegate votes to\n     * @param nonce The contract state required to match the signature\n     * @param expiry The time at which to expire the signature\n     * @param v The recovery byte of the signature\n     * @param r Half of the ECDSA signature pair\n     * @param s Half of the ECDSA signature pair\n     */\n    function delegateBySig(address delegatee, uint nonce, uint expiry, uint8 v, bytes32 r, bytes32 s) public override {\n        // XXX_CHANGED: name => _name\n        bytes32 domainSeparator = keccak256(abi.encode(DOMAIN_TYPEHASH, keccak256(bytes(_name)), getChainId(), address(this)));\n        bytes32 structHash = keccak256(abi.encode(DELEGATION_TYPEHASH, delegatee, nonce, expiry));\n        bytes32 digest = keccak256(abi.encodePacked(\"\\x19\\x01\", domainSeparator, structHash));\n        address signatory = ecrecover(digest, v, r, s);\n        require(signatory != address(0), \"Uni::delegateBySig: invalid signature\");\n        require(nonce == nonces[signatory]++, \"Uni::delegateBySig: invalid nonce\");\n        // XXX: added linter disable\n        // solhint-disable-next-line not-rely-on-time\n        require(block.timestamp <= expiry, \"Uni::delegateBySig: signature expired\");\n        return _delegate(signatory, delegatee);\n    }\n\n    /**\n     * @notice Gets the current votes balance for `account`\n     * @param account The address to get votes balance\n     * @return The number of current votes for `account`\n     */\n    function getCurrentVotes(address account) external view override returns (uint96) {\n        uint32 nCheckpoints = numCheckpoints[account];\n        return nCheckpoints > 0 ? checkpoints[account][nCheckpoints - 1].votes : 0;\n    }\n\n    /**\n     * @notice Determine the prior number of votes for an account as of a block number\n     * @dev Block number must be a finalized block or else this function will revert to prevent misinformation.\n     * @param account The address of the account to check\n     * @param blockNumber The block number to get the vote balance at\n     * @return The number of votes the account had as of the given block\n     */\n    function getPriorVotes(address account, uint blockNumber) public view override returns (uint96) {\n        require(blockNumber < block.number, \"Uni::getPriorVotes: not yet determined\");\n\n        uint32 nCheckpoints = numCheckpoints[account];\n        if (nCheckpoints == 0) {\n            return 0;\n        }\n\n        // First check most recent balance\n        if (checkpoints[account][nCheckpoints - 1].fromBlock <= blockNumber) {\n            return checkpoints[account][nCheckpoints - 1].votes;\n        }\n\n        // Next check implicit zero balance\n        if (checkpoints[account][0].fromBlock > blockNumber) {\n            return 0;\n        }\n\n        uint32 lower = 0;\n        uint32 upper = nCheckpoints - 1;\n        while (upper > lower) {\n            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow\n            Checkpoint memory cp = checkpoints[account][center];\n            if (cp.fromBlock == blockNumber) {\n                return cp.votes;\n            } else if (cp.fromBlock < blockNumber) {\n                lower = center;\n            } else {\n                upper = center - 1;\n            }\n        }\n        return checkpoints[account][lower].votes;\n    }\n\n    function _delegate(address delegator, address delegatee) internal {\n        address currentDelegate = delegates[delegator];\n        uint96 delegatorBalance = balances[delegator];\n        delegates[delegator] = delegatee;\n\n        emit DelegateChanged(delegator, currentDelegate, delegatee);\n\n        _moveDelegates(currentDelegate, delegatee, delegatorBalance);\n    }\n\n    function _transferTokens(address src, address dst, uint96 amount) internal {\n        require(src != address(0), \"Uni::_transferTokens: cannot transfer from the zero address\");\n        require(dst != address(0), \"Uni::_transferTokens: cannot transfer to the zero address\");\n\n        balances[src] = sub96(balances[src], amount, \"Uni::_transferTokens: transfer amount exceeds balance\");\n        balances[dst] = add96(balances[dst], amount, \"Uni::_transferTokens: transfer amount overflows\");\n        emit Transfer(src, dst, amount);\n\n        _moveDelegates(delegates[src], delegates[dst], amount);\n    }\n\n    function _moveDelegates(address srcRep, address dstRep, uint96 amount) internal {\n        if (srcRep != dstRep && amount > 0) {\n            if (srcRep != address(0)) {\n                uint32 srcRepNum = numCheckpoints[srcRep];\n                uint96 srcRepOld = srcRepNum > 0 ? checkpoints[srcRep][srcRepNum - 1].votes : 0;\n                uint96 srcRepNew = sub96(srcRepOld, amount, \"Uni::_moveVotes: vote amount underflows\");\n                _writeCheckpoint(srcRep, srcRepNum, srcRepOld, srcRepNew);\n            }\n\n            if (dstRep != address(0)) {\n                uint32 dstRepNum = numCheckpoints[dstRep];\n                uint96 dstRepOld = dstRepNum > 0 ? checkpoints[dstRep][dstRepNum - 1].votes : 0;\n                uint96 dstRepNew = add96(dstRepOld, amount, \"Uni::_moveVotes: vote amount overflows\");\n                _writeCheckpoint(dstRep, dstRepNum, dstRepOld, dstRepNew);\n            }\n        }\n    }\n\n    function _writeCheckpoint(address delegatee, uint32 nCheckpoints, uint96 oldVotes, uint96 newVotes) internal {\n      uint32 blockNumber = safe32(block.number, \"Uni::_writeCheckpoint: block number exceeds 32 bits\");\n\n      if (nCheckpoints > 0 && checkpoints[delegatee][nCheckpoints - 1].fromBlock == blockNumber) {\n          checkpoints[delegatee][nCheckpoints - 1].votes = newVotes;\n      } else {\n          checkpoints[delegatee][nCheckpoints] = Checkpoint(blockNumber, newVotes);\n          numCheckpoints[delegatee] = nCheckpoints + 1;\n      }\n\n      emit DelegateVotesChanged(delegatee, oldVotes, newVotes);\n    }\n\n    function safe32(uint n, string memory errorMessage) internal pure returns (uint32) {\n        require(n < 2**32, errorMessage);\n        return uint32(n);\n    }\n\n    function safe96(uint n, string memory errorMessage) internal pure returns (uint96) {\n        require(n < 2**96, errorMessage);\n        return uint96(n);\n    }\n\n    function add96(uint96 a, uint96 b, string memory errorMessage) internal pure returns (uint96) {\n        uint96 c = a + b;\n        require(c >= a, errorMessage);\n        return c;\n    }\n\n    function sub96(uint96 a, uint96 b, string memory errorMessage) internal pure returns (uint96) {\n        require(b <= a, errorMessage);\n        return a - b;\n    }\n\n    function getChainId() internal view returns (uint) {\n        uint256 chainId;\n        // XXX: added linter disable\n        // solhint-disable-next-line no-inline-assembly\n        assembly { chainId := chainid() }\n        return chainId;\n    }\n}\n"
    },
    "@ubeswap/governance/contracts/interfaces/IVotingDelegates.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\n/**\n * Interface for a contract that keeps track of voting delegates.\n */\ninterface IVotingDelegates {\n    /// @notice An event thats emitted when an account changes its delegate\n    event DelegateChanged(\n        address indexed delegator,\n        address indexed fromDelegate,\n        address indexed toDelegate\n    );\n\n    /// @notice An event thats emitted when a delegate account's vote balance changes\n    event DelegateVotesChanged(\n        address indexed delegate,\n        uint256 previousBalance,\n        uint256 newBalance\n    );\n\n    /// @notice An event emitted when an account's voting power is transferred.\n    // - If `from` is `address(0)`, power was minted.\n    // - If `to` is `address(0)`, power was burned.\n    event Transfer(address indexed from, address indexed to, uint256 amount);\n\n    /// @notice Name of the contract.\n    // Required for signing.\n    function name() external view returns (string memory);\n\n    /// @notice A record of each accounts delegate\n    function delegates(address delegatee) external view returns (address);\n\n    /**\n     * @notice Delegate votes from `msg.sender` to `delegatee`\n     * @param delegatee The address to delegate votes to\n     */\n    function delegate(address delegatee) external;\n\n    /**\n     * @notice Delegates votes from signatory to `delegatee`\n     * @param delegatee The address to delegate votes to\n     * @param nonce The contract state required to match the signature\n     * @param expiry The time at which to expire the signature\n     * @param v The recovery byte of the signature\n     * @param r Half of the ECDSA signature pair\n     * @param s Half of the ECDSA signature pair\n     */\n    function delegateBySig(\n        address delegatee,\n        uint256 nonce,\n        uint256 expiry,\n        uint8 v,\n        bytes32 r,\n        bytes32 s\n    ) external;\n\n    /**\n     * @notice Get the amount of voting power of an account\n     * @param account The address of the account to get the balance of\n     * @return The amount of voting power held\n     */\n    function votingPower(address account) external view returns (uint96);\n\n    /// @notice Total voting power in existence.\n    function totalVotingPower() external view returns (uint96);\n}\n"
    },
    "@ubeswap/governance/contracts/voting/VotingToken.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\nimport \"../interfaces/INonTransferrableToken.sol\";\nimport \"./VotingPower.sol\";\n\n/**\n * A non-transferrable token that can vote.\n */\ncontract VotingToken is INonTransferrableToken, VotingPower {\n    string private _symbol;\n    uint8 private immutable _decimals;\n\n    /**\n     * @dev Sets the values for `name`, `symbol`, and `decimals`. All three of\n     * these values are immutable: they can only be set once during\n     * construction.\n     */\n    constructor(\n        string memory name_,\n        string memory symbol_,\n        uint8 decimals_\n    ) VotingPower(name_) {\n        _symbol = symbol_;\n        _decimals = decimals_;\n    }\n\n    function name()\n        public\n        view\n        override(INonTransferrableToken, VotingPower)\n        returns (string memory)\n    {\n        return VotingPower.name();\n    }\n\n    function symbol() public view override returns (string memory) {\n        return _symbol;\n    }\n\n    function decimals() public view override returns (uint8) {\n        return _decimals;\n    }\n\n    function totalSupply() public view override returns (uint256) {\n        return totalVotingPower();\n    }\n\n    function balanceOf(address _account)\n        public\n        view\n        override\n        returns (uint256)\n    {\n        return votingPower(_account);\n    }\n}\n"
    },
    "@ubeswap/governance/contracts/interfaces/INonTransferrableToken.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\n/**\n * A token that cannot be transferred.\n */\ninterface INonTransferrableToken {\n    function name() external view returns (string memory);\n\n    function symbol() external view returns (string memory);\n\n    function decimals() external view returns (uint8);\n\n    // Views\n    function totalSupply() external view returns (uint256);\n\n    function balanceOf(address _account) external view returns (uint256);\n}\n"
    },
    "@ubeswap/governance/contracts/voting/TransferrableVotingToken.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.8.3;\n\nimport \"./VotingToken.sol\";\n\ncontract TransferrableVotingToken is VotingToken {\n    /**\n     * @dev Sets the values for `name`, `symbol`, and `decimals`. All three of\n     * these values are immutable: they can only be set once during\n     * construction.\n     * @param initialSupply_ Initial supply of tokens\n     * @param account_ The initial account to grant all the tokens\n     */\n    constructor(\n        string memory name_,\n        string memory symbol_,\n        uint8 decimals_,\n        uint96 initialSupply_,\n        address account_\n    ) VotingToken(name_, symbol_, decimals_) {\n        _mintVotes(account_, initialSupply_);\n    }\n\n    ////////////////////////////////\n    //\n    // The below code is copied from Uniswap's Uni.sol.\n    // Changes are marked with \"XXX\".\n    //\n    ////////////////////////////////\n\n    // XXX: deleted name, symbol, decimals, totalSupply, minter, mintingAllowedAfter,\n    // minimumTimeBetweenMints, mintCap\n\n    // Allowance amounts on behalf of others\n    mapping (address => mapping (address => uint96)) internal allowances;\n\n    // XXX: balances, delegates, Checkpoint, checkpoints,\n    // numCheckpoints, DOMAIN_TYPEHASH, DELEGATION_TYPEHASH\n    // are inherited from VotingPower.sol\n\n    /// @notice The EIP-712 typehash for the permit struct used by the contract\n    bytes32 public constant PERMIT_TYPEHASH = keccak256(\"Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)\");\n\n    // XXX: nonces is inherited from VotingPower.sol\n\n    // XXX: deleted MinterChanged\n\n    // XXX: deleted DelegateChanged, DelegateVotesChanged, Transfer and moved them to IVotingPower\n\n    /// @notice The standard EIP-20 approval event\n    event Approval(address indexed owner, address indexed spender, uint256 amount);\n\n    // XXX: deleted constructor, setMinter, mint\n\n    /**\n     * @notice Get the number of tokens `spender` is approved to spend on behalf of `account`\n     * @param account The address of the account holding the funds\n     * @param spender The address of the account spending the funds\n     * @return The number of tokens approved\n     */\n    function allowance(address account, address spender) external view returns (uint) {\n        return allowances[account][spender];\n    }\n\n    // XXX_ADDED: upgrade to Solidity 0.8.3, which doesn't allow use of uintn(-1)\n    uint256 internal constant MAX_INT = 2**256 - 1;\n    uint96 internal constant MAX_INT_96 = 2**96 - 1;\n\n    /**\n     * @notice Approve `spender` to transfer up to `amount` from `src`\n     * @dev This will overwrite the approval amount for `spender`\n     *  and is subject to issues noted [here](https://eips.ethereum.org/EIPS/eip-20#approve)\n     * @param spender The address of the account which may transfer tokens\n     * @param rawAmount The number of tokens that are approved (2^256-1 means infinite)\n     * @return Whether or not the approval succeeded\n     */\n    function approve(address spender, uint rawAmount) external returns (bool) {\n        uint96 amount;\n        // XXX: uint256(-1) => MAX_INT\n        if (rawAmount == MAX_INT) {\n            // XXX: uint96(-1) => MAX_INT_96\n            amount = MAX_INT_96;\n        } else {\n            amount = safe96(rawAmount, \"Uni::approve: amount exceeds 96 bits\");\n        }\n\n        allowances[msg.sender][spender] = amount;\n\n        emit Approval(msg.sender, spender, amount);\n        return true;\n    }\n\n    /**\n     * @notice Triggers an approval from owner to spends\n     * @param owner The address to approve from\n     * @param spender The address to be approved\n     * @param rawAmount The number of tokens that are approved (2^256-1 means infinite)\n     * @param deadline The time at which to expire the signature\n     * @param v The recovery byte of the signature\n     * @param r Half of the ECDSA signature pair\n     * @param s Half of the ECDSA signature pair\n     */\n    function permit(address owner, address spender, uint rawAmount, uint deadline, uint8 v, bytes32 r, bytes32 s) external {\n        uint96 amount;\n        // XXX: uint256(-1) => MAX_INT\n        if (rawAmount == MAX_INT) {\n            // XXX: uint96(-1) => MAX_INT_oy\n            amount = MAX_INT_96;\n        } else {\n            amount = safe96(rawAmount, \"Uni::permit: amount exceeds 96 bits\");\n        }\n\n        // XXX_CHANGED: name => name()\n        bytes32 domainSeparator = keccak256(abi.encode(DOMAIN_TYPEHASH, keccak256(bytes(name())), getChainId(), address(this)));\n        bytes32 structHash = keccak256(abi.encode(PERMIT_TYPEHASH, owner, spender, rawAmount, nonces[owner]++, deadline));\n        bytes32 digest = keccak256(abi.encodePacked(\"\\x19\\x01\", domainSeparator, structHash));\n        address signatory = ecrecover(digest, v, r, s);\n        require(signatory != address(0), \"Uni::permit: invalid signature\");\n        require(signatory == owner, \"Uni::permit: unauthorized\");\n        // XXX: added linter disable\n        // solhint-disable-next-line not-rely-on-time\n        require(block.timestamp <= deadline, \"Uni::permit: signature expired\");\n\n        allowances[owner][spender] = amount;\n\n        emit Approval(owner, spender, amount);\n    }\n\n    // XXX: deleted balanceOf\n\n    /**\n     * @notice Transfer `amount` tokens from `msg.sender` to `dst`\n     * @param dst The address of the destination account\n     * @param rawAmount The number of tokens to transfer\n     * @return Whether or not the transfer succeeded\n     */\n    function transfer(address dst, uint rawAmount) external returns (bool) {\n        // XXX_ADDED\n        require(\n            dst != address(this),\n            \"TransferrableVotingToken::transfer: cannot send tokens to contract\"\n        );\n        uint96 amount = safe96(rawAmount, \"Uni::transfer: amount exceeds 96 bits\");\n        _transferTokens(msg.sender, dst, amount);\n        return true;\n    }\n\n    /**\n     * @notice Transfer `amount` tokens from `src` to `dst`\n     * @param src The address of the source account\n     * @param dst The address of the destination account\n     * @param rawAmount The number of tokens to transfer\n     * @return Whether or not the transfer succeeded\n     */\n    function transferFrom(address src, address dst, uint rawAmount) external returns (bool) {\n        // XXX_ADDED\n        require(\n            dst != address(this),\n            \"TransferrableVotingToken::transferFrom: cannot send tokens to contract\"\n        );\n        address spender = msg.sender;\n        uint96 spenderAllowance = allowances[src][spender];\n        uint96 amount = safe96(rawAmount, \"Uni::approve: amount exceeds 96 bits\");\n\n        // XXX: uint96(-1) => MAX_INT_96\n        if (spender != src && spenderAllowance != MAX_INT_96) {\n            uint96 newAllowance = sub96(spenderAllowance, amount, \"Uni::transferFrom: transfer amount exceeds spender allowance\");\n            allowances[src][spender] = newAllowance;\n\n            emit Approval(src, spender, newAllowance);\n        }\n\n        _transferTokens(src, dst, amount);\n        return true;\n    }\n\n    // XXX: rest is in VotingPower.sol\n}\n"
    },
    "contracts/token/IPCTToken.sol": {
      "content": "//SPDX-License-Identifier: Apache-2.0\npragma solidity 0.8.4;\n\nimport \"@ubeswap/governance/contracts/voting/TransferrableVotingToken.sol\";\n\ncontract IPCTToken is TransferrableVotingToken {\n    /// @notice The maximum supply of IPCT Tokens.\n    uint96 public constant MAX_SUPPLY = 10_000_000_000e18;\n\n    /**\n     * @notice Construct a new IPCT token\n     * Note: this contract doesn't specify an initial minter, so there is no way new\n     * tokens can get created.\n     * @param _initialOwner The initial account to grant all the tokens\n     */\n    constructor(address _initialOwner)\n        TransferrableVotingToken(\"PactToken\", \"PACT\", 18, MAX_SUPPLY, _initialOwner)\n    {}\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "outputSelection": {
      "*": {
        "*": [
          "abi",
          "evm.bytecode",
          "evm.deployedBytecode",
          "evm.methodIdentifiers",
          "metadata",
          "devdoc",
          "userdoc",
          "storageLayout",
          "evm.gasEstimates",
          "devdoc",
          "userdoc"
        ],
        "": [
          "ast"
        ]
      }
    },
    "metadata": {
      "useLiteralContent": true
    }
  }
}